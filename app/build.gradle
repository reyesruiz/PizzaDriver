apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

android {
    compileSdkVersion 32
    buildToolsVersion '32.0.0'
    def versionPropsFile = file('version.properties')
    Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionPropsFile))
    def mVersionCode = versionProps['VERSION_CODE_NUMBER'].toInteger()
    def major = versionProps['VERSION_MAJOR'].toInteger()
    def minor = versionProps['VERSION_MINOR'].toInteger()
    def patch = versionProps['VERSION_PATCH'].toInteger()
    def build = versionProps['VERSION_BUILD'].toInteger()
    def mVersionName = "v${major}.${minor}.${patch} Build: ${build}"
    def mApplicationId = "com.digitalruiz.pizzadriver"
    def version_name = "v$major.$minor.$patch.$build"
    setProperty("archivesBaseName","${mApplicationId}-${version_name}")




    defaultConfig {
        applicationId mApplicationId
        minSdkVersion 26
        targetSdkVersion 32
        versionCode mVersionCode
        versionName mVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${mApplicationId}-${version_name}-${variant.buildType.name}.apk"
        }
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [MAPS_API_KEY_FOR_MANIFEST: localProperties['places_api_key']]
            buildConfigField("String", "API_KEY", "\"" + localProperties['places_api_key'] + "\"")
            debuggable true

        }

        debug {
            manifestPlaceholders = [MAPS_API_KEY_FOR_MANIFEST: localProperties['places_api_key']]
            buildConfigField("String", "API_KEY", "\"" + localProperties['places_api_key'] + "\"")
        }
    }


    buildFeatures {
        viewBinding true
    }
    lint {
        abortOnError false
    }
    namespace 'com.digitalruiz.pizzadriver'

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'org.json:json:20220320'
    implementation 'com.google.android.libraries.places:places:2.6.0'
    implementation 'com.google.android.gms:play-services-maps:18.0.2'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.google.truth:truth:1.1.3"

    // Core library
    androidTestImplementation 'androidx.test:core:1.4.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'

    //Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation "com.google.truth:truth:1.1.3"

    // Espresso dependencies
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'




    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'com.google.android.material:material:1.6.1'

}
